     A                                      INDARA
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R GESTIONBAS
     A                                 23  2'F2=Utilisateur'
     A                                      COLOR(BLU)
     A                                 23 22'F3=Fin'
     A                                      COLOR(BLU)
     A                                 23 36'F4=Recherche'
     A                                      COLOR(BLU)
     A                                 23 53'F6=Services'
     A                                      COLOR(BLU)
     A                                 23 67'F12=Abandon'
     A                                      COLOR(BLU)
     A            E1MSGERR      30     22  3ALIAS(ECRANMESSAGEERREUR)
     A                                      COLOR(RED)
     A                                      DSPATR(RI)
     A  82                                  DSPATR(ND)
     A            E1MSGINFO     40     22 41ALIAS(ECRANMESSAGEINFO)
     A                                      DSPATR(RI)
     A  83                                  DSPATR(ND)
     A                                      COLOR(TRQ)
     A            EVEOBJ        10A    24 70COLOR(PNK)
     A                                      ALIAS(ECRANVERBEOBJET)
     A                                 24  2'F23=rech mult-critères'            
     A                                      COLOR(BLU)                          
     A          R FMSELECT                  OVERLAY
     A                                      CF02(01 'Utilisateur')
     A                                      CF04(04 'Recherche')
     A                                      CF06(06 'Services')
     A                                      CA12(12 'Abandon')
     A                                      CA03(03 'Fin')
     A                                      CA23(23 'Recherche multi-crit')
     A                                      RTNCSRLOC(&EFORMA &EZONCU)
     A                                      CSRLOC(ENOLIG ENOCOL)
     A            ENOLIG         3S 0H      ALIAS(ECRANDEPLACERCURSEURLIGNE)
     A            ENOCOL         3S 0H      ALIAS(ECRANDEPLACERCURSEURCOLONNE)
     A            EFORMA        10A  H
     A            EZONCU        10A  H      ALIAS(ECRANZONECURSEUR)
     A            ELIACT        30A     1 28DSPATR(HI)
     A                                      ALIAS(ECRANLIBELLEACTION)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                 10 18'Code société . . :'                
     A                                      DSPATR(UL)
     A            ECOSTE         2   B 10 38
     A                                      ALIAS(ECRANCODESOCIETE)
     A                                      TEXT('Code société')
     A                                 12 18'Code client . . .:'                
     A                                      DSPATR(UL)
     A            ECOCLI         9   B 12 38
     A                                      ALIAS(ECRANCODECLIENT)
     A                                      TEXT('Code client')
     A                                 14 18'Date inventaire .:'                
     A                                      DSPATR(UL)
     A            EDAINV          L  B 14 38ALIAS(ECRANDATEINVENTAIRE)
     A                                      DATFMT(*JOB)
     A            ELISTE        20A     1  2ALIAS(ECRANLIBELLESOCIETE)          
     A          R GESTIONSFL                SFL
     A            ELQINV         4Y 0B  9 66ALIAS(ECRANLIGNEQUANTITEINVENTAIRE)
     A                                      TEXT('ECRANLIGNEQUANTITEINVENTAIRE')
     A                                      EDTCDE(Z)
     A  43                                  DSPATR(PR)
     A            ELCDAR        20   O  9  2ALIAS(ECRANLIGNECODEARTICLE)
     A                                      TEXT('ECRANLIGNECODEARTICLE')
     A  70                                  COLOR(BLU)
     A            ELLIAR        30      9 24ALIAS(ECRANLIGNELIBELLE1ARTICLE)
     A                                      TEXT('ECRANLIGNELIBELLE1ARTICLE')
     A  70                                  COLOR(BLU)
     A            ELQTHE         4Y 0O  9 56ALIAS(ECRANLIGNEQUANTITETHEORIE)
     A                                      TEXT('ECRANLIGNEQUANTITETHEORIE')
     A                                      EDTCDE(Z)
     A                                      COLOR(BLU)
     A          R GESTIONCTL                SFLCTL(GESTIONSFL)
     A  51                                  SFLDSP
     A  52                                  SFLDSPCTL
     A  53                                  SFLCLR
     A  54                                  SFLEND(*SCRBAR *MORE)
     A                                      SFLPAG(13)
     A                                      SFLSIZ(100)
     A                                      OVERLAY
     A                                      CF02(01 'Utilisateur')
     A                                      CF06(06 'Services')
     A                                      CA12(12 'Abandon')
     A                                      CA03(03 'Fin')
     A            ERDNB2         4S 0B  2  5SFLRCDNBR
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      ALIAS(ECRANLIGNESOUSFICHIER)
     A            ELISTE        20A     1  2ALIAS(ECRANLIBELLESOCIETE)
     A            ELIACT        30A     1 29DSPATR(HI)
     A                                      ALIAS(ECRANLIBELLEACTION)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  3  2'Société :'
     A                                      COLOR(WHT)                          
     A            ECOSTE         2   O  3 12          
     A                                      ALIAS(ECRANCODESOCIETE)
     A            ELISOC        20   O  3 16ALIAS(ECRANLIBELLESOCIETESELECT)
     A                                  4  2'Client :'
     A                                      COLOR(WHT)
     A            ECOCLI         9   O  4 11          
     A                                      ALIAS(ECRANCODECLIENT)
     A            ECLISO        30   O  4 21ALIAS(ECRANCLIENTLIBELLE)
     A                                  5  2'Date inventaire :'
     A                                      COLOR(WHT)
     A            EDAINV          L  O  5 21ALIAS(ECRANDATEINVENTAIRE)
     A                                      DATFMT(*JOB)
     A                                  8  2'Code article        '
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8 24'Libellé article             '
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8 56'THEO '                             
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8 66'INVEN '                            
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7 56'   QUANTITE   '                    
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A          R FMFIN
     A                                      CA12
     A                                      BLINK
     A                                      CLRL(*NO)
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      PROTECT
     A            ECOFO1         1A  O 13 36OVRDTA
     A                                      ALIAS(ECRANFINCHOIX1)
     A                                 13 38'. Mise à jour et retour écran de g-
     A                                      arde  '
     A            ECOFO2         1A  O 14 36OVRDTA
     A                                      ALIAS(ECRANFINCHOIX2)
     A                                 14 38'. Retour écran de garde sans mise -
     A                                      à jour'
     A            ECOFO3         1   O 15 36OVRDTA
     A                                      ALIAS(ECRANFINCHOIX3)
     A                                 15 38'. Mise à jour et fin de programme -
     A                                            '
     A            ECOFO4         1A  O 16 36OVRDTA
     A                                      ALIAS(ECRANFINCHOIX4)
     A                                 16 38'. Fin de programme sans mise à jou-
     A                                      r     '
     A                                 17 34'  5 . Reprise                     -
     A                                                '
     A                                 10 32'                                  -
     A                                                    '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 11 32' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 12 32' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 13 32' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 14 32' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 15 32' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 16 32' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 17 32' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 18 32' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 19 32' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 20 32'                                  -
     A                                                    '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 11 79' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 12 79' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 13 79' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 14 79' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 15 79' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 16 79' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 17 79' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 18 79' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 19 79' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 19 34'                F12=Abandon       -
     A                                                '
     A                                      COLOR(BLU)
     A                                 11 34'               Fin de travail     -
     A                                                '
     A                                      DSPATR(HI)
     A                                 15 34' '
     A                                 16 34' '
     A                                 18 34'                                  -
     A                                                '
     A                                 12 34'                                  -
     A                                                '
     A            ECHOIF         1Y 0B 13 34OVRDTA
     A                                      CHECK(ER)
     A                                      EDTCDE(4)
     A                                      ALIAS(ECRANFINCHOIXACTION)
     A                                 14 34' '