/*--------------------------------------------------------------------------*/
/*                                                                          */
/*       GESTION DES LIVRAISONS DE CONSIGNES                               */
/*                                                                          */
/*--------------------------------------------------------------------------*/
/* NOM        : KCOP100CL              TYPE : Programme                     */
/*                                                                          */
/* TITRE      : GESTION ARTICLES PUBLICITAIRES : LIVRAISON CLIENT           */
/*                                                                          */
/* FONCTIONS  : - Ce programme permet la gestion des articles consignes     */
/* à livrer/retourné aux clients grâce à l'appl de :                        */
/*   - KCOP10 : Interactif (DSPF)                                           */
/*        Choix de la livraison parmis une liste                            */
/*                                                                          */
/*   - KCOP11 : Interactif (DSPF)                                           */
/*        Saisie des quantités livrées/retournées pour une livraison        */
/*                                                                          */
/*   - KCOP12 : Batch                                                       */
/*       Ecriture des données dans les tables                               */
/*         - VRESTK (Insert mouvement de stocks)                            */
/*         - KCOENT (Entête des livraisons de consignes)                    */
/*         - KCOLIG (Lignes des livraisons de consignes)                    */
/*         - KCOCUM (Cumul des consignes par client)                        */
/*                                                                          */
/*   - KCOP13 : PRTF                                                        */
/*        Edition de la livraison                                           */
/*                                                                          */
/* APPELE PAR : MONITEUR                                                    */
/*                                                                          */
/* PARAMETRES :                                                             */
/*   - d'appel   : .&COVER                                                  */
/*                 .&COOBJ                                                  */
/*                 .&LIACT                                                  */
/*   - retournés :                                                          */
/*                                                                          */
/* APPEL DE   :                                                             */
/*                                                                          */
/*                                                                          */
/* ECRIT DU   : 02/12/2024            PAR : ANg (Antilles Glaces)           */
/*       AU   : 02/12/2024                                                  */
/*                                                                          */
/* -- variables de travail                                                  */
/*                                                                          */
/*        &COSTE   - Code société                                           */
/*        &LISOC   - Libellé société                                        */
/*        &COVER   - Code verbe action                                      */
/*        &COOBJ   - Code objet action                                      */
/*        &LIACT   - Libellé action                                         */
/*        &COCLI   - Code client                                            */
/*        &LICLI   - Libellé client                                         */
/*        &COREP   - Code représentant                                      */
/*        &LIREP   - Libellé représentant                                   */
/*        &COSPE   - Code spécifique                                        */
/*        &LISPE   - Libellé spécifique                                     */
/*        &NUEDI   - Numéro édition                                         */
/*        &CORET   - Code retour                                            */
/*        &BIFIC   - Bibliothèques des fichiers                             */
/*        &BISRC   - Bibliothèques des sources                              */
/*        &SPPVA   - Constante: SPPVALSNEW                                  */
/*        &USER    - Utilisateur                                            */
/*                                                                          */
/*        &SAICA   - Saisie Code Article                                    */
/*        &SAILA   - Saisie Libelle Article                                 */
/*        &SAIQA   - Saisie Quantite Article                                */
/*        &SAICU   - Saisie Code Unite                                      */
/*        &SAIPU   - Saisie Prix Unitaire Article                           */
/*        &SAIPT   - Saisie Prix Total                                      */
/*        &SAIEM   - Saisie Emplacement                                     */
/*        &SAILT   - Saisie Lot                                             */
/*        &SAIEL   - Saisie Element                                         */
/*        &SAIDL   - Saisie Designation Lot                                 */
/*                                                                          */
/*        Prog : K£PIMP                                                     */
/*        &COEDI   - code de l'édition                                      */
/*        &COMOD   - code du module                                         */
/*        &COPRF   - code profil utilisateur                                */
/*        &NOUTQ   - nom de la file de sortie                               */
/*        &NBEXJ   - nombre d'exemplaires : jour                            */
/*        &NBEXS   - nombre d'exemplaires : semaine                         */
/*        &NBEXM   - nombre d'exemplaires : mois                            */
/*        &SUSPE   - édition à suspendre (*YES/*NO)                         */
/*        &CONSE   - édition à conserver (*YES/*NO)                         */
/*                                                                          */
/*        Prog : VSLMVT2                                                    */
/*        &COSTE   - Code société                                           */
/*        &LIMIT   - Code de dépot, de ... à ...                            */
/*        &COECR   - Code écran                                             */
/*        &COPRO   - Code profil                                            */
/*--------------------------------------------------------------------------*/

    PGM        PARM(&COVER &COOBJ &LIACT)


/* - Declaration des variables */

/* -- paramètres d'appel des programme RPG*/

DCL        VAR(&DATLIV)     TYPE(*CHAR)     LEN(10)
DCL        VAR(&COCLI)      TYPE(*CHAR)     LEN(9)
DCL        VAR(&LICLI)      TYPE(*CHAR)     LEN(30)
DCL        VAR(&COREP)      TYPE(*CHAR)     LEN(3)
DCL        VAR(&LIREP)      TYPE(*CHAR)     LEN(25)
DCL        VAR(&COSPE)      TYPE(*CHAR)     LEN(3)
DCL        VAR(&LISPE)      TYPE(*CHAR)     LEN(25)
DCL        VAR(&NUEDI)      TYPE(*DEC)      LEN(8 0)
DCL        VAR(&CORET)      TYPE(*CHAR)     LEN(1)
DCL        VAR(&JOB)        TYPE(*CHAR)     LEN(10)


/* -- variables de travail */

DCL        VAR(&BIFIC)     TYPE(*CHAR)     LEN(10)
DCL        VAR(&BISRC)     TYPE(*CHAR)     LEN(10)
DCL        VAR(&SPPVA)     TYPE(*CHAR)     LEN(10)
DCL        VAR(&USER)      TYPE(*CHAR)     LEN(10)


/* -- variables : pour l'acceptation des mouvements de stock        */

DCL        VAR(&LIMIT) TYPE(*CHAR) LEN(06)
DCL        VAR(&COECR) TYPE(*CHAR) LEN(10)
DCL        VAR(&COPRO) TYPE(*CHAR) LEN(10)

/* -- variables pour la gestion des éditions PRTF */
DCL        VAR(&COEDI) TYPE(*CHAR) LEN(8)
DCL        VAR(&NOUTQ) TYPE(*CHAR) LEN(10)
DCL        VAR(&NBEXJ) TYPE(*CHAR) LEN(2)
DCL        VAR(&NBEXS) TYPE(*CHAR) LEN(2)
DCL        VAR(&NBEXM) TYPE(*CHAR) LEN(2)
DCL        VAR(&SUSPE) TYPE(*CHAR) LEN(4)
DCL        VAR(&CONSE) TYPE(*CHAR) LEN(4)
DCL        VAR(&USRDTA) TYPE(*CHAR) LEN(10)


/*----------------------------------------------------------------------*/
/*                             PROGRAMME                                */
/*----------------------------------------------------------------------*/

/*                                                                      */
/*                             INITIALISATIONS                          */
/*                                                                      */

/* Initialisation des variables */
CHGVAR     VAR(&SPPVA)     VALUE('PTFV220601')
/* TODO: A remplacer avant la mise en prod par la ligne suivante */
/*CHGVAR     VAR(&SPPVA)          VALUE('SPPVALSNEW') */
CHGVAR     VAR(&CORET)     VALUE('1')


/* - Récupération de l'utilisateur demandeur et du JOB*/

RTVJOBA    JOB(&JOB) USER(&USER)


/* - Récupération du nom de la bibliothèque des fichiers */

RTVOBJD    OBJ(VRESTK) OBJTYPE(*FILE) RTNLIB(&BIFIC)


/* - Récupération du nom de la bibliothèque des fichiers sources */

RTVMBRD    FILE(QDDSSRC) MBR(KPBF01) RTNLIB(&BISRC)
MONMSG     MSGID(CPF9815) EXEC(DO)
   CHGVAR     VAR(&BISRC) VALUE(&SPPVA)
ENDDO


/* Création table de travail KPBF01 si non existante */

CHKOBJ     OBJ(&BIFIC/KPBF01) OBJTYPE(*FILE)
MONMSG     MSGID(CPF9801) EXEC(DO)
    RUNSQLSTM  SRCFILE(&BISRC/QDDSSRC) SRCMBR(KPBF01) DFTRDBCOL(&BIFIC)
ENDDO


/* Création table de travail KPBFHT si non existante */

CHKOBJ     OBJ(&BIFIC/KPBFHT) OBJTYPE(*FILE)
MONMSG     MSGID(CPF9801) EXEC(DO)
    RUNSQLSTM  SRCFILE(&BISRC/QDDSSRC) SRCMBR(KPBFHT) DFTRDBCOL(&BIFIC)
ENDDO


/*                                                                      */
/*                      BOUCLE D APPEL DES PROGRAMMES                   */
/*                                                                      */


DOWHILE COND((&CORET *EQ '1') *OR (&CORET *EQ '2'))


    /* - Remise à blanc du fichier KPBF01 */

    CLRPFM     FILE(&BIFIC/KPBF01)


    /* - Appel Programme interactif : KPBP10 */

    CALL PGM(KPBP10) PARM(&COVER &COOBJ &LIACT &BIFIC +
    &CORET &COSTE &CODEP &COCLI &LICLI &COREP &LIREP &COSPE &LISPE &DATLIV)


    /* - Si demande de mise à jour */

    IF COND((&CORET *EQ '1') *OR (&CORET *EQ '3')) THEN(DO)
        /* -- Appel programme BATCH : KPBP11  */

        CALL PGM(KPBP11) PARM(&COSTE &CODEP &DATLIV +
                        &COCLI &LICLI &COREP &LIREP &COSPE &LISPE &NUEDI)


        /* -- Appel programme PRTF : KPBP12 */

        /* - Gestion des options d'impressions                          */
        /*   - d'appel   : . COEDI : code de l'édition                  */
        /*                 . COMOD : code du module                     */
        /*                 . COPRF : code profil utilisateur            */
        /*   - retournés : . NOUTQ : nom de la file de sortie           */
        /*                 . NBEXJ : nombre d'exemplaires : jour        */
        /*                 . NBEXS : nombre d'exemplaires : semaine     */
        /*                 . NBEXM : nombre d'exemplaires : mois        */
        /*                 . SUSPE : édition à suspendre (*YES/*NO)     */
        /*                 . CONSE : édition à conserver (*YES/*NO)     */

        CHGVAR     VAR(&COEDI) VALUE('KPBP12PM')
        CALL       PGM(K£PIMP) PARM(&COEDI 'PB' &USER +
                                &NOUTQ &NBEXJ &NBEXS &NBEXM &SUSPE &CONSE)
        /* Définit les paramètres d'impression                              */
        /* - OUTQ spécifie la file d'attente de sortie                      */
        /* - COPIES spécifie le nombre de copies de chaque page             */
        /* - HOLD permet de mettre en attente l'impression jusqu'à ce       */
        /*   qu'une commande libère l'impression                            */
        /* - SAVE permet de sauvegarder les données imprimées               */
        /*   dans un fichier spécifié                                       */
        /* - USRDTA permet de spécifier des données à                       */
        /*  imprimer en tant qu'en-tête de chaque page                      */

        CHGVAR     VAR(&USRDTA) VALUE(&COSTE *CAT 'ST' *CAT '-----W')

        OVRPRTF    FILE(&COEDI) OUTQ(&NOUTQ) PRTTXT('QQQ_' *CAT &USRDTA) +
                   COPIES(&NBEXJ) HOLD(&SUSPE) +
                   SAVE(&CONSE) USRDTA(&USRDTA)

        CALL PGM(KPBP12) PARM(&COSTE &COCLI &COREP &LIREP &COSPE &LISPE +
                                &NUEDI &DATLIV)


        /* Suppression des paramètres d'impression */

        DLTOVR     FILE(&COEDI)


        /* - Acceptation des mouvements de stock en attente présents dans   */
        /*   le fichier VRESTK (supprime les lignes dans VRESTK)            */

        CHGVAR     VAR(&COEDI) VALUE('VSLMVTP')

        CALL       PGM(K£PIMP) PARM(&COEDI 'PB' &USER &NOUTQ +
                            &NBEXJ &NBEXS &NBEXM &SUSPE &CONSE)

        OVRPRTF    FILE(&COEDI) OUTQ(&NOUTQ) COPIES(&NBEXJ) +
                            HOLD(&SUSPE) SAVE(&CONSE)

        CHGVAR     VAR(&COECR) VALUE(&JOB)
        CHGVAR     VAR(&LIMIT) VALUE(&CODEP *TCAT &CODEP)
        CHGVAR     VAR(&COPRO) VALUE('**********')
        CALL       PGM(VSLMVT2) PARM(&COSTE &LIMIT &COECR &COPRO)
        SNDMSG     MSG('Intégration des mouvements de stock +
                            terminée') TOUSR(&USER)


        /* Suppression des paramètres d'impression */

        DLTOVR     FILE(&COEDI)


    ENDDO

ENDDO



/* - Fin de programme  */
 FINPGM:
            ENDPGM

